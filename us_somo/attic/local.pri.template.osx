# Profile include file for local use
#  Copy file to local.pri and change variables to match your installation

# ---- user configuration area ----
# **** make sure the following variables are set for your system ***

# uncomment exactly one of these:
# DEBUGORRELEASE = debug
DEBUGORRELEASE = release

# path of US3 source base
US3PATH       = /Users/eb/us3

# path of US3_SOMO source base
US3SOMOPATH   = /Users/eb/us3_somo

# path of the QT (on OSX QTPATH can be left undefined)
QTPATH        = 
QWTPATH       = /src/qwt-5.2.3

# for windows also define the MINGWPATH
MINGWPATH     =

# ---- these below might need to be modified in unusual circumstances ----
QWT3DPATH     = $$US3PATH/qwtplot3d-qt4
# define this if you are not using qwtplot3d-qt4 as the qwtplot3d lib for linux & osx
QWT3DLIBNAME  = 

# ---- probably should not be changed

count( QWT3DLIBNAME, 0 ) {
   QWT3DLIBNAME = qwtplot3d-qt4
}

CONFIG     += qt thread warn
contains( DEBUGORRELEASE, debug ) {
   CONFIG    += debug
} else {
   CONFIG    += release
}

INCLUDEPATH  += $$US3SOMOPATH/develop/src
INCLUDEPATH  += $$QTPATH/include
# some qwt5 have libraries in src/ some in include/; having extra (possibly non-existant) include paths is not a problem
INCLUDEPATH  += $$QWTPATH/include $$QWTPATH/src
INCLUDEPATH  += $$QWT3DPATH/include

# ---- system specific definitions
include ( uname.pri )


# OSX also reports UNIX
contains( DEFINES, "OSX" ) {
} else {
    unix {
      # Local flags
      #QMAKE_CXXFLAGS_DEBUG += -pg
      #QMAKE_LFLAGS_DEBUG += -pg
    
      #If you want your compiles to do a globus run on bcf, uncomment the following lines:
      # Be sure to add the environment variable MAKEFLAGS=-j44
      #QMAKE_CXX  = g++
      #QMAKE_CC   = g++
      #QMAKE_MOC  = $(QTDIR)/bin/moc
      #QMAKE_UIC  = $(QTDIR)/bin/uic
      #QMAKE_LINK = g++
    
# these should really be in qmake.conf 
      QMAKE_CXXFLAGS_RELEASE = -O3
      QMAKE_CFLAGS_RELEASE   = -O3
   }
}

win32 {

#   QMAKESPEC   = win32-g++-4.6
  
  DEFINES      += MINGW

  contains( DEBUGORRELEASE, debug ) {
    QWTLIB      = $$QWTPATH/lib/libqwtd5.a
  } else {
    QWTLIB      = $$QWTPATH/lib/libqwt5.a
  }
}

macx {
##  CONFIG      += x86_64 x86 app_bundle
  CONFIG      *= x86_64
  DEFINES     += MAC OSX
  INCLUDEPATH += /System/Libraries/Frameworks/OpenGL.framework/Headers
  INCLUDEPATH += /Library/Frameworks/QtCore.framework/Headers
  INCLUDEPATH += /Library/Frameworks/QtGui.framework/Headers
  INCLUDEPATH += /Library/Frameworks/QtOpenGL.framework/Headers
  INCLUDEPATH += /usr/X11R6/include
  INCLUDEPATH += /usr/X11R6/include/GL
  LIBS        += -L/System/Library/Frameworks/OpenGL.framework/Libraries
  LIBS        += -L$$US3PATH/lib
  LIBS        += -l$$QWT3DLIBNAME
  LIBS        += -framework QtOpenGL
  LIBS        += -L$$QWTPATH/lib -lqwt
#  X11LIB       = -L/usr/X11R6/lib -lXau -lX11
}
